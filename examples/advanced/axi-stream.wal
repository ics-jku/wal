;; This examples analyzes the uart->axi converter from https://github.com/fcayci/vhdl-axis-uart.
(load "uart-axi.fst" t)

(alias clk uut_rx.clk)
(alias ready uut_rx.m_axis_tready)
(alias valid uut_rx.m_axis_tvalid)
(alias data uut_rx.m_axis_tdata)

(set [byte-arrived 0]
     [bytes-dropped 0]
     [byte-delay '()])

;; Iterate over the complete trace and print all arrived,  read, and delayed byes.
;; Also calculate the average delay between the time a byte
;; is available and the time it is read by the AXI sink.
(whenever clk
	  ;; triggered whenever new valid data is available
	  (when (|| (&& (! valid@-2) valid) ;; rising edge on valid
		    (&& (!= data@-2 data) valid)) ;; no rising edge on valid but data change
	    (set [byte-arrived INDEX])
	    (printf "%8d: new byte 0x%03x " INDEX data))
	  
	  ;; triggered whenever a transaction happens
	  (when (&& valid ready)
	    ;; calculate the delay between when the byte was available and now
	    ;; divide by 2 to get number of clock cycles
	    (set [ack-delay (/ (- INDEX byte-arrived) 2)]
		 [byte-delay (+ byte-delay ack-delay)])
	    ;; chceck if byte was not read instantly
	    (if (= ack-delay 0)
		(print "[OK]")
		(printf "[DELAY %d]\n" ack-delay)))
	  
	  ;; triggered whenever the data changes whithout being acknowledged
	  (when (&& (!= data data@2) valid (! ready))
	    (inc bytes-dropped)
	    (print "[DROPPED]")))

(when bytes-dropped
  (printf "\nWARNING: %d bytes dropped due to an unavailable AXI sink!\n" bytes-dropped))

(printf "Average delay until byte acknowlede %d cycles\n" (average byte-delay))
